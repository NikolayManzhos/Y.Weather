apply plugin: 'jacoco-android'

task jacocoTest(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"


    reports {
        xml.enabled = true
        html.enabled = true
    }

    // Exclude Dagger 2 and other generated code
    def fileFilter = [
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
            '**/*_*Factory*.*',
            '**/*_Module*.*',
            '**/*_ViewBinding*.*',
            '**/*_MembersInjector*.*',
            '**/*_Provide*.*',
            '**/Dagger*.*',
            '**/di/*',
            '**/di/modules/*',
            '**/data/entities/*',
            '**/domain/models/*',
            '**/presentation/main/home_screen/view_model/*'
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug/com/example", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files([
            "${buildDir}/jacoco/testDebugUnitTest.exec",
            fileTree(dir: "${buildDir}/outputs/code-coverage/connected/").matching {
                include '*.ec'
            }
    ])

    // Always regenerate report
    outputs.upToDateWhen { false }
}